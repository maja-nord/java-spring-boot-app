plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'maven-publish'
    id 'com.palantir.docker' version "0.25.0"
}

group = 'maja-nord'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.ibm.db2:jcc'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri 'https://maven.pkg.github.com/maja-nord/java-spring-boot-app'
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('GPR_USER')
                password = project.findProperty('gpr.key') ?: System.getenv('GPR_KEY')
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java

            artifact bootJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

docker {
    name 'docker.pkg.github.com/maja-nord/java-spring-boot-app/java-spring-boot-app'
    dockerfile file('Dockerfile.runtime')
    files tasks.bootJar.outputs
}